# -*-mode:sh-*- vim:ft=zsh
# =============================================================================
#
# ~/.zshrc
#
# =============================================================================

# OPTIMIZATION: zprof is for profiling. Comment it out for daily use.
# zmodload zsh/zprof

# --- Core Settings & Cache Directory -----------------------------------------
export DEFAULT_USER="${USER}"
export DISABLE_UPDATE_PROMPT="false"
export UPDATE_ZSH_DAYS=7
export KEYTIMEOUT=1 # Reduces editor key-press delay
export ZSH_CACHE_DIR="$HOME/.cache/zsh" # Standardized cache location
if [[ ! -d "$ZSH_CACHE_DIR" ]]; then mkdir -p "$ZSH_CACHE_DIR/completions"; fi

# --- ZPM Plugin Manager ------------------------------------------------------
if [[ ! -f "${XDG_DATA_HOME:-$HOME/.local/share}/zsh/plugins/@zpm/zpm.zsh" ]]; then
  git clone --recursive https://github.com/zpm-zsh/zpm "${XDG_DATA_HOME:-$HOME/.local/share}/zsh/plugins/@zpm"
fi
source "${XDG_DATA_HOME:-$HOME/.local/share}/zsh/plugins/@zpm/zpm.zsh"

# --- ZPM Plugins -------------------------------------------------------------
zpm load @gh/romkatv/zsh-defer # Load defer utility first
zpm load @omz # Pull in OMZ libraries
zpm load \
  @omz/lib/functions \
  @omz/lib/prompt_info_functions \
  @omz/lib/git \
  @omz/lib/compfix \
  @omz/lib/grep \
  @omz/lib/termsupport \
  @omz/lib/spectrum \
  @omz/lib/key-bindings \
  @omz/lib/completion

zpm load \
  @omz/git,async \
  @omz/colored-man-pages,async \
  @omz/vi-mode

zpm if-not ssh load \
  @gh/zdharma-continuum/fast-syntax-highlighting,async \
  @gh/zpm-zsh/zsh-autosuggestions,async \
  @gh/zsh-users/zsh-completions,async \
  @omz/poetry,async \
  @omz/scala,async \
  @omz/sbt,async

zpm load custom

# --- Plugin Configurations ---------------------------------------------------
# vi-mode
VI_MODE_RESET_PROMPT_ON_MODE_CHANGE=true
VI_MODE_SET_CURSOR=true
bindkey -M viins 'jk' vi-cmd-mode
bindkey -M vicmd 'V' edit-command-line

# zsh-autosuggestions
export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=#610061,bg=#D4D4D4" # see https://webaim.org/resources/contrastchecker/
export ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE="50"
export ZSH_AUTOSUGGEST_USE_ASYNC=true

# --- FZF (Fuzzy Finder) Configuration ----------------------------------------
export FZF_DEFAULT_COMMAND='fd --type f --color=never --ignore-case --no-ignore --hidden --follow'
export FZF_DEFAULT_OPTS="
--bind 'f1:execute(bat --paging=always --color=always {})'
--bind 'f2:execute($EDITOR {})+abort'
--bind 'ctrl-y:execute-silent(echo -n {} | pbcopy)+abort'
--bind 'ctrl-z:toggle-up'
--bind '?:toggle-preview'
--preview='bat --number --color=always --style=numbers,changes {} | head -700' --preview-window='right:50%:hidden'
--color header:italic
--header '<CTRL-Y>:copy - <F2>:open - <tab|CTRL-Z>:select'"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
export FZF_ALT_C_COMMAND='fd --type d --color=never --no-ignore --ignore-case --hidden --follow '
export FZF_CTRL_R_OPTS="$FZF_DEFAULT_OPTS"

# --- Shell Hooks -------------------------------------------------------------
# Safely add hooks only if the 'fre' command exists
autoload -Uz add-zsh-hook
if (( $+commands[fre] )); then
  function fre_chpwd() { fre --add "$(pwd)"; }
  function fre_preexec () {
    local tmpcmd="$3"
    local cand_fname=${(Q)${(z)tmpcmd}[-1]}
    [[ -f "$cand_fname" ]] && fre --add "$(realpath "$cand_fname")"
  }
  add-zsh-hook chpwd fre_chpwd
  add-zsh-hook preexec fre_preexec
fi

# --- Deferred Initializations (for max speed) --------------------------------
# Load these tools and scripts AFTER the prompt is displayed

# Tooling
zsh-defer eval "$(zoxide init zsh)"
zsh-defer eval "$(atuin init zsh)"

zsh-defer eval "$(scmpuff init -s --aliases=false)"

# Starship Prompt
export STARSHIP_LOG=error
eval "$(starship init zsh)"

# --- Completions -------------------------------------------------------------
# All fpath modifications and completion setups go here, before the single compinit call.

# Deferrable completions
_cache_git_machete_completions() {
  local cache_file="$ZSH_CACHE_DIR/completions/_git_machete"
  if [[ ! -f "$cache_file" ]] || [[ "$(command -v git-machete)" -nt "$cache_file" ]]; then
    git machete completion zsh > "$cache_file"
  fi
  zsh-defer source "$cache_file"
}
_cache_git_machete_completions
unset -f _cache_git_machete_completions


# Synchronous completions
fpath=("/Users/tkolleh/Library/Application Support/ScalaCli/completions/zsh" $fpath)

# Initialize the completion system ONCE.
autoload -Uz compinit
compinit -C

# --- Final Exports -----------------------------------------------------------
export ICLOUD_DIR="$HOME/Library/Mobile Documents/com~apple~CloudDocs/"
export CHEZMOI_SOURCE_PATH="$HOME/.local/share/chezmoi"
