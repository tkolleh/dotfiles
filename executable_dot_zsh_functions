#!/usr/bin/env zsh
# -*-mode:zsh-*- vim:ft=zsh
#
# ~/.zsh_functions
# ====================================
#

# Print path to command if exist
function cmd_path() {
  if [[ $ZSH_VERSION ]]; then
    whence -cp "$1" 2> /dev/null
  else  # bash
    type -P "$1"  # No output if not in $PATH
  fi
}

#
# Alias helper functions
#
# Overwrite alias if argument is an aliase otherwi
function __alias () {
  if (( $+aliases[$1] )); then
    unalias "$1"
    alias "$1"="$2"
  elif type -w "$1" >/dev/null 2>&1; then
    local x=$(type -w "$1")
    echo "'$1' cannot be assigned as an alias, already exists as: '$x'" >&2
  else
    alias "$1"="$2"
  fi
}

# Print paths on new line and count
function syspath(){
   echo $PATH | sed 's/:/\n/g' | sort | uniq -c
}

# Find string in files using ripgrep as the filter with fzf interface. Adds files 
# to quickfix list if more than one is selected.
# see: https://junegunn.github.io/fzf/tips/ripgrep-integration/
# ripgrep->fzf->vim [QUERY]
fif() (
  RELOAD='reload:rg --unrestricted --column --color=always --regexp {q} || :'
  OPENER='if [[ $FZF_SELECT_COUNT -eq 0 ]]; then
            nvim {1} +{2}     # No selection. Open the current line in Vim.
          else
            nvim +cw -q {+f}  # Build quickfix list for the selected items.
          fi'
  fzf --disabled --ansi --multi \
      --bind "start:$RELOAD" --bind "change:$RELOAD" \
      --bind "enter:become:$OPENER" \
      --bind "ctrl-o:execute:$OPENER" \
      --bind 'alt-a:select-all,alt-d:deselect-all,?:toggle-preview' \
      --delimiter : \
      --preview 'bat --style=full --color=always --highlight-line {2} {1}' \
      --preview-window '~4,+{2}+4/3,<80(up)' \
      --query "$*"
)

# fbc - fuzzy Find Branch or tag to Checkout
# checkout git branch (including remote branches), sorted by most recent commit, limit 30 last branches
# reference: https://github.com/junegunn/fzf/wiki/examples#git
function fcb() {
  local tags branches target
  branches=$(
    git --no-pager branch --all \
      --format="%(if)%(HEAD)%(then)%(else)%(if:equals=HEAD)%(refname:strip=3)%(then)%(else)%1B[0;34;1mbranch%09%1B[m%(refname:short)%(end)%(end)" \
    | sed '/^$/d') || return
  tags=$(
    git --no-pager tag | awk '{print "\x1b[35;1mtag\x1b[m\t" $1}') || return
  target=$(
    (echo "$branches"; echo "$tags") |
    fzf --no-hscroll --no-multi -n 2 \
        --ansi) || return
  git checkout $(awk '{print $2}' <<<"$target" )
}

alias glNoGraph='git log --color=always --format="%C(auto)%h%d %s %C(black)%C(bold)%cr% C(auto)%an"'
_gitLogLineToHash="echo {} | grep -o '[a-f0-9]\{7\}' | head -1"
_viewGitLogLine="$_gitLogLineToHash | xargs -I % sh -c 'git -c diff.external=difft show --ext-diff %'"

# fcoc_preview - checkout git commit with previews
function fcoc_preview() {
  local commit
  commit=$(glNoGraph |
    fzf --no-sort --reverse --tiebreak=index --no-multi \
      --ansi --preview="$_viewGitLogLine") &&
    git checkout $(echo "$commit" | sed "s/ .*//")
}

# fshow_preview - git commit browser with previews
function glshow() {
  glNoGraph "$@" |
    fzf --no-sort --reverse --tiebreak=index --no-multi \
      --ansi --preview="$_viewGitLogLine" \
      --header "enter to view, alt-y to copy hash" \
      --bind "enter:execute:$_viewGitLogLine" \
      --bind "alt-y:execute:$_gitLogLineToHash | pbcopy"
}

# fcs - get git commit sha
# example usage: git rebase -i `fcs`
function fcs() {
  local commits commit
  commits=$(git log --color=always --pretty=oneline --abbrev-commit --reverse) &&
    commit=$(echo "$commits" | fzf --tac +s +m -e --ansi --reverse) &&
    echo -n $(echo "$commit" | sed "s/ .*//")
}

# Check the content length of a file and print size in MB
function contentlength() {
  if [[ ! -z "$1" ]]; then
    curl -sLI $1 | awk 'tolower($1) ~ /content-length/ {print $1, ($2/1000000), "MB"}'
  else
    echo "Enter URL to file"
  fi
}

# fuzzy find alias
function fzfa() {
  alias | bat --paging=never -n -f -l bash | fzf --ansi
}

# Kubernetes namesapce visualization tool
# https://github.com/viralpoetry/kubesurveyor
function k8nsv() {
  if [[ ! -z "$1" ]]; then
    echo "Saving namespace data for $1"
    kubesurveyor $1 --insecure --save > namespace.yaml
    cat namespace.yaml| kubesurveyor $1 --load --out png
    trash "$1"
  else
    echo "Namespace required"
  fi
}

function print_color_band() {
  echo -n "
    # Check term support for true color. Prints a color band showing gradients
    # if there is a difference between gradients i.e no smooth color band, then
    # there is something wrong with true color support.
    #
    # See clicolors() for full list of console colors
  "
  awk 'BEGIN{
      s="/\\/\\/\\/\\/\\"; s=s s s s s s s s;
      for (colnum = 0; colnum<77; colnum++) {
          r = 255-(colnum*255/76);
          g = (colnum*510/76);
          b = (colnum*255/76);
          if (g>255) g = 510-g;
          printf "\033[48;2;%d;%d;%dm", r,g,b;
          printf "\033[38;2;%d;%d;%dm", 255-r,255-g,255-b;
          printf "%s\033[0m", substr(s,colnum+1,1);
      }
      printf "\n";
  }'
}

# colors, a lot of colors!
function clicolors() {
    local i=1
    local c=""
    for color in {000..255}; do
        c=$c"$FG[$color]$colorâœ”$reset_color  ";
        if [ `expr $i % 8` -eq 0 ]; then
            c=$c"\n"
        fi
        i=`expr $i + 1`
    done;
    echo $c | sed 's/%//g' | sed 's/{//g' | sed 's/}//g' | sed '$s/..$//';
}

function edit_dot_files() {
  if (( ${+commands[chezmoi]} )) && (( ${+commands[zed]} )) && [[ -d $CHEZMOI_SOURCE_PATH ]]; then
    nvim $CHEZMOI_SOURCE_PATH
    chezmoi apply
  fi
}

function update_term_tools() {
  if (( ${+commands[atuin]} )); then
    gum spin --title "Syncing atuin history..." -- atuin sync || :
  fi
  if (( ${+commands[zpm]} )); then
    gum spin --title "Cleaning zpm..." -- zpm clean || :
    gum spin --title "Upgrading zpm..." -- zpm upgrade || :
  fi
  if (( ${+commands[cs]} )); then
    gum spin --title "Updating coursier..." -- cs update --quiet || :
  fi
  if (( ${+commands[pipx]} )); then
    gum spin --title "Upgrading all pipx packages..." -- pipx upgrade-all --quite || :
  fi
  if (( ${+commands[node]} )); then
    gum spin --title "Updating global npm packages..." -- npm --global update || :
  fi
  if (( ${+commands[goose]} )); then
    gum spin --title "Updating block goose..." -- goose update || :
  fi
}

function has_tldr_page() {
  tldr --language en "$1" > /dev/null 2>&1
}

function has_man_page() {
  man "$1" > /dev/null 2>&1
}

function help() {
  # Get some help for this command prefering the
  #
  if [[ -z "$1" ]]; then
    echo "No command specified. The input string is empty."
  else
    if has_tldr_page "$1"; then
      tldr "$@"
      return
    fi
    if has_man_page "$1"; then
        if (( ${+commands[batman]} )); then
          batman "$@"
          return
        else
          man "$1"
          return
        fi
    fi
  fi
  "$@" --help 2>&1 | bat --plain --language=help
}

# Debug what the heck is going on with gpg
function debug-gpg(){
  echo RELOADAGENT | gpg-connect-agent
  echo "test" | gpg --clearsign
}

function sbt_clear_cache(){
  echo -e "\nRun with '-a' to clean all cache files\n"

  if [[ $1 == "-a" ]]; then
    echo -e "\nEmptying the maven cache (this may take some time to rebuild)\n"
    rm -fvr ~/.m2/repository

    echo -e "\nEmptying the ivy cache (this may take some time to rebuild)\n"
    rm -fvr ~/.ivy2/cache/*
    find ~/.sbt ~/.ivy2 -name "*.lock" -print -delete
    find ~/.sbt ~/.ivy2 -name "ivydata-*.properties" -print -delete


    echo -e "\nRemoving all sbt project related class files\n"
    rm -fvr ~/.sbt/1.0/plugins/target
    rm -fvr ~/.sbt/1.0/plugins/project/target
    rm -fvr ~/.sbt/1.0/target
    rm -fvr ~/.sbt/0.13/plugins/target
    rm -fvr ~/.sbt/0.13/plugins/project/target
    rm -fvr ~/.sbt/0.13/target
  fi

  echo -e "\nEmptying the idl cache\n"
  rm -fvr ./idl/lib
  rm -fvr ~/.idl-sbt-cache
  echo -e "\nRunning 'sbt clean update'\n"
  rm -fvr ~/.idl-sbt-cache
  rm -fvr target
  rm -fvr .metals
  rm -fvr .bsp
  rm -fvr .bloop
  rm -fvr ./project/target
  rm -fvr ./project/project/target
  sbt clean update
}

# Search all commits in heads or remotes for changes matching pattern
function grep_refs(){
  echo "\t======= Searching '${1}' for '${2}' ========="
  git grep -i "${2}" $(git for-each-ref --format='%(refname)' refs/"${1}")
}

function print_jvm_versions(){
  echo -e "\nPrinting runtime and tool versions of the JVM.\n"
  local arr=("java" "scala" "mvn" "sbt")
  for i in "${arr[@]}"; do
      echo -e "\n---- ${i} version ----\n"
      if (( ${+commands[$i]} )); then
        $i -version
      fi
  done
}

function extract_file_from_image(){
  if [ $# -eq 3 ]; then
    echo -e "\nExtracting file [$2] from image [$1]"
    echo -e "Saving file to [$3]\n"
    local image=$1
    local source_path=$2
    local destination_path=$3
    local container_id=$(docker create "$image")
    docker cp "$container_id:$source_path" "$destination_path"
    docker rm "$container_id"
  else
    echo -e "\nRequires <image id> <source path> <destination path>"
  fi
}

function is_installed_by_brew(){
  # Check if the file exists
  local file="$1"
  if [[ ! -e "$file" ]]; then
    # Return an exit code without closing the shell while having -e set.
    echo "File $file does not exist." && (return 1)
  fi

  if (( ${+commands[rg]} )) && (( ${+commands[brew]} )); then
    # Count the number of lines in the file
    local pattern_cnt=$(wc -l "$file" | awk '{print $1}')
    local rg_output=$(brew list --versions | rg -f "$file")
    local line_count=$(echo "$rg_output" | sort | uniq | wc -l | awk '{print $1}')

    # Compare line count with pattern_cnt
    if (( "$line_count" == "$pattern_cnt" )); then
      echo "The line count [$line_count] equals pattern_cnt [$pattern_cnt]." && (return 0)
    else
      echo "The line count [$line_count] does not equal pattern_cnt [$pattern_cnt]." && (return 2)
    fi
  else
    echo "Requires ripgrep and brew to be installed." && (return 1)
  fi
}

function select_from_brew_info() {
  # Select fields from brew info JSON of installed homebrew formulas.
  # Defaults to 'name, full_name, desc, homepage' when field names are not provided.
  if ! (( ${+commands[brew]} )) || ! (( ${+commands[duckdb]} )); then
    echo "Requires brew and duckdb to be installed" && (return 1)
  fi

  local _fields
  local default_fields="name, full_name, \"desc\""
  local brew_info_json_path=$TMPDIR/brew_info_.json
  local query_rslt_path=$TMPDIR/brew_info_result.csv
  local _fields=$default_fields

  if [[ -z "$1" ]]; then
    _fields=$default_fields
  else
    _fields="$1"
  fi

  local query=$(print -r -- "COPY (
      select $_fields
      from read_json_auto('${brew_info_json_path}', ignore_errors := true)
    ) TO '${query_rslt_path}';
  ")

  brew info --installed --json > $brew_info_json_path

  echo -e "\nQuerying brew info table of installed formulas"
  echo -e "\tQuery: [$query]"
  echo -e "\tInput path: $brew_info_json_path"
  echo -e "\tOutput path: $query_rslt_path"

  duckdb -c "${query}"
  bat $query_rslt_path
}

function print_pr_basics() {
  if ! (( ${+commands[gh]} )); then
    echo "Requires gh to be installed."
    return 1
  fi

  if [[ "$1" == "slack" ]]; then
    local template='
#### {{.title}}

- Number: {{.number}}
- Title: {{.title}}
- State: {{.state}}
- Is Draft: {{.isDraft}}
- Updated At: {{.updatedAt}}
- Merge Status: {{.mergeStateStatus}}
- URL: (PR:{{.number}})[{{.url | printf "%.120s"}}]
'
    gh pr view --json number,title,url,updatedAt,state,author,isDraft,mergeable,mergeStateStatus --template "$template" | glow --width 120 --
  else
    local template='
#### {{.title}}

| Attribute       | Value                      |
|-----------------|----------------------------|
| State           | {{.state}}                 |
| Is Draft PR     | {{.isDraft}}               |
| Updated At      | {{.updatedAt}}             |
| Merge Status    | {{.mergeStateStatus}}      |
| URL             | (PR:{{.number}})[{{.url | printf "%.120s"}}] |
 '
    gh pr view --json number,title,url,updatedAt,state,author,isDraft,mergeable,mergeStateStatus --template "$template" | glow --width 120 --
  fi
}

function print_pr_failed_checks(){
  local template='
  Failed Checks:
  --------------
  {{- range . }}
  {{- if eq .state "FAILURE" }}
  {{ .name }}: {{ .description }}
  {{- end }}
  {{- end }}
  '
  if (( ${+commands[gh]} )); then
      gh pr checks $1 --json name,description,state --template $template
      return 0
  else
      echo "Requires gh to be installed." && (return 1)
      return 1
  fi
}

function print_pr_review_request(){
  local ask="Please review the following PR(s):"
  local header=$(print_pr_basics)
  local body=$(print_pr_failed_checks)
  if ! (( $+commands[glow] )); then
    print -u2 "glow command not found. Please install glow to view markdown."
    return 1
  fi
  if ! (( $+commands[gmktemp] )); then
    print -u2 "gmktemp command not found. Please install coreutils."
    return 1
  fi
  local tmp_md=$(gmktemp --suffix=.md pr_review_XXXXXX)
  printf "%s\n%s\n%s\n" "$ask" "$header" "$body" > "$tmp_md"
  glow "$tmp_md"
}

function ssh_ghostty() {
  infocmp -x | ssh -p $1 $2 -- tic -x -
  ssh -p $1 $2
}

function gcm() {
  # This script is used to write a conventional commit message.
  # It prompts the user to choose the type of commit as specified in the
  # conventional commit spec. And then prompts for the summary and detailed
  # description of the message and uses the values provided. as the summary and
  # details of the message.
  #
  # If you want to add a simpler version of this script to your dotfiles, use:
  #
  # alias gcm='git commit -m "$(gum input)" -m "$(gum write)"'

  # if [ -z "$(git status -s -uno | grep -v '^ ' | awk '{print $2}')" ]; then
  #     gum confirm "Stage all?" && git add .
  # fi
  unalias gcm > /dev/null 2>&1 || true # TODO: Remove zpm git plugin

  local TYPE=$(gum choose "fix" "feat" "docs" "style" "refactor" "test" "chore" "revert")
  local DEFAULT_SCOPE_JIRA=$(git branch --show-current | rg --only-matching --regexp '[a-zA-Z]{3}\-[0-9]{4}')
  local SCOPE=$(gum input --placeholder "scope" --value "${DEFAULT_SCOPE_JIRA}")

  # Since the scope is optional, wrap it in parentheses if it has a value.
  test -n "$SCOPE" && SCOPE="($SCOPE)"

  # Pre-populate the input with the type(scope): so that the user may change it
  SUMMARY=$(gum input --value "$TYPE$SCOPE: " --placeholder "Summary of this change")
  DESCRIPTION=$(gum write --placeholder "Details of this change")

  # Commit these changes if user confirms
  gum confirm "Commit changes?" && git commit -m "$SUMMARY" -m "$DESCRIPTION"
}

# aig: Display the output of the last llm command using glow, with paging for long outputs.
# Requirements: 'glow' and 'llm' must be installed.
# - If the first argument is 'prev', prints the last llm logs output and exits.
# - Otherwise, runs 'llm' with provided arguments (output discarded).
# - Fetches the last llm command output into a temporary markdown file.
# - If output exceeds 20 lines, appends paging info and uses glow's pager.
# - Otherwise, displays output directly with glow.
# - Suggests installing coreutils for better mktemp experience if not present.
function aig() {
  if ! (( ${+commands[glow]} && ${+commands[llm]} )); then
    echo "Error: Requires glow and llm to be installed"
    return 1
  fi

  if [[ "$1" == "prev" ]]; then
    local prev_command_path=$(gmktemp --suffix=.md)
    llm logs --count 1 > "$prev_command_path"
    glow --pager $prev_command_path
    return
  fi

  gum spin --show-output --title="Talking to the AI... " --spinner="meter" -- llm "$@" > /dev/null
  local max_lines=20

  local last_command_path
  if (( ${+commands[gmktemp]} )); then
    last_command_path=$(gmktemp --suffix=.md)
  else
    last_command_path=$(mktemp)
    printf "\n%s\n" "Install coreutils for a better terminal experience: \`brew install coreutils\`"
  fi

  llm logs --count 1 > "$last_command_path"
  local linecount=$(wc -l "$last_command_path" | awk '{print $1}')
  if (( linecount > $max_lines )); then
    printf "\n%s\n" "
>Paging output because the last command output is longer than $max_lines lines:
>* Path to paged output: ${last_command_path}
>* Print original output: \`llm logs --count 1\`
    " >> $last_command_path
    glow --pager "$last_command_path"
  else
    glow "$last_command_path"
  fi
}
